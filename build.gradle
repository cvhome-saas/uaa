plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'com.asrevo'
version = '0.0.1-SNAPSHOT'
description = 'uaa'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'org.springframework.cloud:spring-cloud-starter'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

node {
    version = '20.11.0'
    npmVersion = '10.2.4'
    download = true
}

// Configure the automatically created npmInstall task
tasks.named('npmInstall', com.github.gradle.node.npm.task.NpmTask) {
    workingDir = file('uaa-fe')
}

// Create a new task to run 'npm run build'
tasks.register('angularBuild', com.github.gradle.node.npm.task.NpmTask) {
    dependsOn tasks.named('npmInstall')
    workingDir = file('uaa-fe')
    args = ['run', 'build']
}

tasks.register('npmAgGridInstall', com.github.gradle.node.npm.task.NpmTask) {
    dependsOn tasks.named('npmInstall')
    workingDir = file('uaa-fe')
    args = ['install', 'ag-grid-angular', 'ag-grid-community']
}

bootJar {
    dependsOn tasks.named('angularBuild')
    from ("${projectDir}/uaa-fe/dist/uaa-fe") {
        into 'static'
    }
}
